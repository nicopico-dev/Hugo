apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

def version = loadProperties('version.properties')
def secureFolder = System.getenv('SECURE_FOLDER')
def keystore = loadProperties("$secureFolder/hugo_keystore.properties")

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId 'fr.nicopico.hugo'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode Integer.parseInt(version.code)
        versionName "${version.major}.${version.minor}.${version.patch}"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        resConfigs 'fr'
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
            storePassword keystore.storePassword
            keyAlias keystore.keyAlias
            keyPassword keystore.keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'env'
    productFlavors {
        prod {
            dimension 'env'
            buildConfigField 'String', 'FIRESTORE_ROOT', '"/users"'
        }

        dev {
            dimension 'env'
            applicationIdSuffix '.dev'
            buildConfigField 'String', 'FIRESTORE_ROOT', '"/dev_users"'
        }
    }

    packagingOptions {
        exclude 'META-INF/lib_main.kotlin_module'
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    ext.support_version = '27.1.1'
    ext.coroutines_version = '0.22.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'androidx.core:core-ktx:0.3'

    implementation 'com.google.firebase:firebase-analytics:15.0.2'
    implementation 'com.google.firebase:firebase-auth:15.1.0'
    implementation 'com.google.firebase:firebase-core:15.0.2'
    implementation 'com.google.firebase:firebase-firestore:16.0.0'
    implementation 'com.google.firebase:firebase-perf:15.1.0'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:15.0.1'
    implementation 'com.firebaseui:firebase-ui-auth:3.2.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.2'

    implementation 'com.github.pardom:redux-kotlin:1.2.1'
    implementation 'com.github.pardom:redux-logger-kotlin:1.2.4'
    implementation ('com.xwray:groupie:2.0.3') { transitive = false }
    implementation ('com.xwray:groupie-kotlin-android-extensions:2.0.3') { transitive = false }

    testImplementation 'junit:junit:4.12'
    testImplementation 'io.mockk:mockk:1.6.3'
    testImplementation 'com.natpryce:hamkrest:1.4.2.2'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'